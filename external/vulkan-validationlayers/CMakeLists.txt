# cmake file for vulkan-validationlayers

if (NOT DE_DEFS)
	message(FATAL_ERROR "Include Defs.cmake")
endif ()

if (EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/src/layers/VkLayer_khronos_validation.def")
	set(DEFAULT_VULKAN_VALIDATIONLAYERS_SRC_PATH ${CMAKE_CURRENT_SOURCE_DIR}/src)
else ()
	set(DEFAULT_VULKAN_VALIDATIONLAYERS_SRC_PATH "../vulkan-validationlayers")
endif ()

set(VULKAN_VALIDATIONLAYERS_SRC_PATH ${DEFAULT_VULKAN_VALIDATIONLAYERS_SRC_PATH} CACHE STRING "Path to vulkan-validationlayers source tree")
if (IS_ABSOLUTE ${VULKAN_VALIDATIONLAYERS_SRC_PATH})
	set(VULKAN_VALIDATIONLAYERS_ABS_PATH ${VULKAN_VALIDATIONLAYERS_SRC_PATH})
else ()
	set(VULKAN_VALIDATIONLAYERS_ABS_PATH "${CMAKE_SOURCE_DIR}/${VULKAN_VALIDATIONLAYERS_SRC_PATH}")
endif ()

# VVL have issues building on Windows caused by build type mismatch in dependencies.
# We exclude them from the Windows build for now. VK-GL-CTS issue: 5052
if (NOT DE_OS_IS_ANDROID AND NOT DE_OS_IS_IOS AND NOT DE_OS_IS_WIN32 AND NOT DE_OS_IS_OHOS AND "${SBT_STRIPPED}" STREQUAL "" AND EXISTS ${VULKAN_VALIDATIONLAYERS_ABS_PATH}/external)
	if (EXISTS ${VULKAN_VALIDATIONLAYERS_ABS_PATH}/CMakeLists.txt)
		message(STATUS "vulkan-validationlayers found")
		set(CMAKE_C_FLAGS ${DE_3RD_PARTY_C_FLAGS})
		set(CMAKE_CXX_FLAGS ${DE_3RD_PARTY_CXX_FLAGS})

		# Make sure VVL uses its own deps.  The Vulkan headers / SPIR-V Tools
		# in CTS may update more frequently than VVL, and this avoids
		# unnecessary churn.  The external deps are downloaded by
		# fetch_sources.py as part of the post-checkout process.
		# Include helper.cmake if it exists (generated by update_deps.py)
		if (EXISTS ${VULKAN_VALIDATIONLAYERS_ABS_PATH}/external/helper.cmake)
			include(${VULKAN_VALIDATIONLAYERS_ABS_PATH}/external/helper.cmake)

			# Set CMake package search paths to use local builds instead of system packages
			# This ensures VVL uses the exact versions of dependencies built alongside it
			if (VULKAN_HEADERS_INSTALL_DIR AND EXISTS "${VULKAN_HEADERS_INSTALL_DIR}/share/cmake/VulkanHeaders/VulkanHeadersConfig.cmake")
				set(VulkanHeaders_DIR "${VULKAN_HEADERS_INSTALL_DIR}/share/cmake/VulkanHeaders" CACHE PATH "Path to VulkanHeaders CMake config" FORCE)
				message(STATUS "Using local VulkanHeaders from ${VulkanHeaders_DIR}")
			endif()

			if (VULKAN_UTILITY_LIBRARIES_INSTALL_DIR AND EXISTS "${VULKAN_UTILITY_LIBRARIES_INSTALL_DIR}/lib/cmake/VulkanUtilityLibraries/VulkanUtilityLibrariesConfig.cmake")
				set(VulkanUtilityLibraries_DIR "${VULKAN_UTILITY_LIBRARIES_INSTALL_DIR}/lib/cmake/VulkanUtilityLibraries" CACHE PATH "Path to VulkanUtilityLibraries CMake config" FORCE)
			endif()

			if (SPIRV_HEADERS_INSTALL_DIR AND EXISTS "${SPIRV_HEADERS_INSTALL_DIR}/share/cmake/SPIRV-Headers/SPIRV-HeadersConfig.cmake")
				set(SPIRV-Headers_DIR "${SPIRV_HEADERS_INSTALL_DIR}/share/cmake/SPIRV-Headers" CACHE PATH "Path to SPIRV-Headers CMake config" FORCE)
			endif()

			if (SPIRV_TOOLS_INSTALL_DIR AND EXISTS "${SPIRV_TOOLS_INSTALL_DIR}/lib/cmake/SPIRV-Tools-opt/SPIRV-Tools-optConfig.cmake")
				set(SPIRV-Tools-opt_DIR "${SPIRV_TOOLS_INSTALL_DIR}/lib/cmake/SPIRV-Tools-opt" CACHE PATH "Path to SPIRV-Tools-opt CMake config" FORCE)
			endif()
		endif()

		# Remove dependencies to things not needed for the validation layers themselves
		set(BUILD_WSI_XCB_SUPPORT OFF)
		set(BUILD_WSI_XLIB_SUPPORT OFF)
		set(BUILD_WSI_WAYLAND_SUPPORT OFF)

		add_subdirectory(${VULKAN_VALIDATIONLAYERS_ABS_PATH} vulkan-validationlayers)
	else ()
		message(FATAL_ERROR "vulkan-validationlayers not found")
	endif ()
endif ()
